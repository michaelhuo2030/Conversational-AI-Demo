plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
}

def baseProps = new File(rootProject.projectDir.absolutePath + File.separator + "gradle.properties")
def properties = new Properties()
if (baseProps.exists()) {
    baseProps.withReader('UTF-8') { reader ->
        properties.load(reader)
    }
}

final def USE_LOCAL_SDK = properties.getProperty("USE_LOCAL_SDK").toBoolean()

def localSdkPath = "agora-sdk"

android {
    namespace 'io.agora.scene.common'
    compileSdk rootProject.ext.compileSdkVersion
    resourcePrefix "common_"

    defaultConfig {
        minSdk rootProject.ext.minSdkVersion
        buildConfigField("String", "TOOLBOX_SERVER_HOST", "\"${project.getProperty("TOOLBOX_SERVER_HOST")}\"")
        buildConfigField("String", "BASIC_AUTH_KEY", "\"${project.getProperty("BASIC_AUTH_KEY")}\"")
        buildConfigField("String", "BASIC_AUTH_SECRET", "\"${project.getProperty("BASIC_AUTH_SECRET")}\"")
        buildConfigField("String", "LLM_URL", "\"${project.getProperty("LLM_URL")}\"")
        buildConfigField("String", "LLM_API_KEY", "\"${project.getProperty("LLM_API_KEY")}\"")
        buildConfigField("String", "LLM_PARRAMS", "\"${project.properties["LLM_PARRAMS"].toString().replace('"', '\\"')}\"")
        buildConfigField("String", "LLM_SYSTEM_MESSAGES", "\"${project.properties["LLM_SYSTEM_MESSAGES"].toString().replace('"', '\\"')}\"")
        buildConfigField("String", "TTS_VENDOR", "\"${project.getProperty("TTS_VENDOR")}\"")
        buildConfigField "String", "TTS_PARAMS", "\"${project.properties['TTS_PARAMS'].toString().replace('"', '\\"')}\""
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = '17'
    }
    if (USE_LOCAL_SDK) {
        sourceSets {
            main {
                jniLibs.srcDirs += "${localSdkPath}"
            }
        }
    }
}

dependencies {
    api libs.androidx.core.ktx
    api libs.androidx.appcompat
    api libs.material
    api libs.androidx.activity
    api libs.androidx.constraintlayout
    api libs.androidx.viewbinding
    api libs.androidx.lifecycle.process
    api libs.navigation.fragment.ktx
    api libs.navigation.ui.ktx
    api libs.splashscreen

    api libs.okhttp.core
    api libs.okhttp.logging.interceptor
    api libs.retrofit
    api libs.converter.gson
    api libs.gson
    api libs.mmkv
    api libs.xlog
    api libs.bugly
    api libs.multidex

    api libs.commons.codec
    if (USE_LOCAL_SDK) {
        api fileTree(dir: "${localSdkPath}", include: ['*.jar'])
    } else {
        api(libs.agora.rtc) {
            exclude group: 'io.agora.rtc', module: 'full-screen-sharing'
        }
    }
    api libs.agora.rtm
}